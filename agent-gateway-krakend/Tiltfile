# -*- mode: Python -*-

version_settings(constraint='>=0.23.0')

# Increase the default k8s upsert timeout to accommodate CRD installations
update_settings(k8s_upsert_timeout_secs=300)


# Configure Tilt to work with AI Gateway Operator's custom AiGateway CRDs
k8s_kind(
    '^AgentGateway$',
    # Operator creates pods asynchronously after AiGateway CRD creation and Tilt
    # must wait for operator-managed pods rather than assuming immediate readiness
    pod_readiness='wait',
)

def agent_gateway_krakend_install(version="0.2.0", operator=True, instance=True):
    if operator:
        agent_gateway_krakend_operator_install(version=version)
    if instance:
        agent_gateway_krakend_instance_install()


def agent_gateway_krakend_operator_install(version="0.1.4"):
    """
    Installs the agent gateway krakend operators into your cluster.
    """

    install_url = "https://github.com/agentic-layer/agent-gateway-krakend-operator/releases/download/v%s/install.yaml"

    wait_cmd = """
kubectl wait --for=condition=Available --timeout=300s -n agent-gateway-krakend-operator-system deployment/agent-gateway-krakend-operator-controller-manager 1>&2
"""

    delete_cmd = [
        "kubectl", "delete", "--ignore-not-found",

        # Kubernetes is prone to deadlocks when you delete CRDs and namespaces at the same time:
        # - The Namespace controller thinks it's responsible for deleting all resources in its namespace.
        # - The CRD controller deletes the API routes for deleting resources.
        # So the namespace controller blocks and eventually times out.
        #
        # To prevent this from slowing down 'tilt down', we do a --wait=false when deleting
        # CRDs+Namespaces together.
        "--wait=false",
        "-f", install_url % version
    ]

    k8s_custom_deploy(
        'agent-gateway-krakend-operator',
        deps=[],
        apply_cmd="""
set -ex
kubectl apply -f %s -o yaml
set +e
""" % (install_url % version) + wait_cmd,
        delete_cmd=delete_cmd,
    )

    k8s_resource(
        'agent-gateway-krakend-operator',
        labels=['agentic-layer'],
        resource_deps=['agent-runtime']
    )


def agent_gateway_krakend_instance_install():
    """
    Deploys an Agent Gateway instance into your cluster.
    """

    # Get the directory of this Tiltfile to reference files relative to the extension
    extension_dir = os.path.dirname(__file__)

    k8s_yaml(os.path.join(extension_dir, 'agent-gateway.yaml'))

    k8s_resource(
        'agent-gateway',
        labels=['agentic-layer'],
        resource_deps=['agent-gateway-krakend-operator']
    )
